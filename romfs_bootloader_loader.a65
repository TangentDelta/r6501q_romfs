;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;ROMFS address equates
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
ROMBSE	= $F000		;ROMFS page base address
MEDDSC	= ROMBSE + $100
ETYCNT	= MEDDSC + $01	;ROMFS directory entry count
FILDIR	= MEDDSC + $10
SIGNAT	= MEDDSC + $02

LDRADR = $0100		;Address in RAM where the bootloader is stored (and jumped into)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;System register equates
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
PORTA	= $00		;Port A data register

MCR	= $14		;Mode Control Register

SCCR	= $15		;Serial Communications Control Register
SCSR	= $16		;Serial Communications Status Register
SCDAT	= $17		;Serial Channel Data Register

LATCHAL	= $18		;Lower Latch A (write)
LATCHAH	= $19		;Upper Latch A (write)
COUNTAH	= $19		;Upper Counter A (read)
COUNTAL	= $1A		;Lower Counter A (read)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;System Hardware Equates
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
IOPAGE		= $EF00		;R65X1Q SBC Glitchbus I/O page

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;Zilog SCC Equates
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SCCC		= IOPAGE+0	;SCC Command Register A
SCCD		= IOPAGE+1	;SCC Data Register A

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;Zero-page variables
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
PTR	= $40
PTRLO	= PTR
PTRHI	= PTR+1

USRIRQ	= $40		;Put a JMP instruction here to the user-defined IRQ

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;Miscellaneous Equates
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SW2MASK	= $3F		;Mask for SW2 bits of Port A
BRMASK	= $30		;Mask for SW2 positions 3 and 4
RFSMSK	= $0F		;Mask for SW2 positions 0, 1, 2, and 3

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;Miscellaneous Constants
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
LDRSZE	= (LDREND - LOADER)	;Bootloader binary size


.segment "ROM_CODE"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;SETUP -- Start of the bootloader
;
;Falls through to SFTRST.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

SETUP:	SEI		;Disable interrupts
	CLD		;Clear decimal arithmetic mode.
	LDX	#$FF	;Set up stack pointer
	TXS

	LDA	#$00	;Set Mode Control Register: full address mode,
	STA	MCR	;Port D tristate, Port B latch disabled, both
			;timers in inverval mode.

	LDA	#$C0	;Configure Serial Communications Channel
	STA	SCCR	;Enable XMTR/RCVR, async, 8N1

;Init the SCC
	LDX	#((ISEND-INISCC))	;X is the number of bytes to write out
	LDY	#$00			;Y is the index into the SCC init sequence

INILUP:	LDA	INISCC,Y	;Get the next SCC command byte in the sequence
	STA	SCCC		;Store it to the SCC's command register

	INY
	DEX
	BNE	INILUP		;Do it again if we haven't reached the end of the sequence (X = 0)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;SETBR -- Set the serial channel bitrate
;
;This routine reads from Port A and uses the value to 
;select a bitrate for the current SCC channel. The 
;speed will depend on the frequency supplied to the SCC
;on the ~RTXC pins.
;
;
;On the Glitch Works R65X1Q SBC, Port A bit 5 is connected
;to SW2 position 3, bit 4 is connect to SW2 position 4.
;
;The bitrate table is stored at BRTAB.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SETBR:	LDA	PORTA		;Get Port A contents
	AND	#BRMASK		;Mask off bitrate switches 
				;SW2-3 = PA5, SW2-4 = PA4
	CLC
	ROR			;SW2-3 = bit 4, SW2-4 = bit 3
	ROR			;SW2-3 = bit 3, SW2-4 = bit 2
	ROR			;SW2-3 = bit 2, SW2-4 = bit 1
	TAX			;X = high byte offset into BRTAB
	LDA	#$0C		;Select SCC WR13 (time constant high byte)
	STA	SCCC
	LDA	BRTAB,X		;A = high byte of time constant
	STA	SCCC

	INX			;X = low byte offset into BRTAB
	LDA	#$0D		;Select SCC WR12 (time constant low byte)
	STA	SCCC
	LDA	BRTAB,X		;A = low byte of time constant
	STA	SCCC


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;BLCOPY -- Bootloader Copy
;
;Copies the ROMFS bootloader into RAM and jumps to it.
;
;X is used as the index.
;
;The bootloader cannot be more than 255 bytes in size!
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
BLCOPY:	LDX	#$00
CPYLUP:	LDA	LOADER,X	;Bootloader copy loop
	STA	LDRADR,X
	INX
	CPX	#LDRSZE		;At end of bootloader?
	BNE	CPYLUP		;No, continue loading the loader

BLJUMP:	JMP	LDRADR		;Jump into the bootloader in RAM!

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;Zilog SCC Init Sequence
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
INISCC:	.byte	$09,$C0		;Reset hardware

	.byte	$04,$44
	.byte	$03,$C1
	.byte	$05,$EA

	.byte	$0B,$56

	
;	.byte	$0C,$0E		;9600 baud (This gets over-written)
;	.byte	$0D,$00


	.byte	$0E,$01

	.byte	$0F,$00
ISEND:				;Init sequence end pointer

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;Serial Channel Bitrate Table
;
;See SETBR for usage. The values in this table are obtained
;  using the following formula:
;  ;(SCK/BR*2*(D))-2
;Where:
;  SCK = Serial Clock (4.9152MHz)
;  BR  = Desired Baudrate (9600 baud)
;  D   = Clock Divider (/16)
;
;Noted bitrates are for a 4.9152MHz clock
;
;TODO: Have the assembler calculate these for me
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
BRTAB:	.word	$000E		;9600  bps
	.word	$0006		;19200 bps
	.word	$0002		;38400 bps
	.word	$0002		;38400 bps

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;The bootloader binary
;The bootloader MUST be compiled first!
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
LOADER:
.incbin "loader.bin"
LDREND:			;Bootloader end address

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;NMI/Reset/IRQ Vectors
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
.segment "VECTORS"

.word	USRIRQ		;$FFFA/$FFFB = NMI
.word	SETUP		;$FFFC/$FFFD = RESET
.word	USRIRQ		;$FFFE/$FFFF = IRQ/BRK